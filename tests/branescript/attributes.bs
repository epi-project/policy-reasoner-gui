// File that showcases some attribute usage

import hello_world;   // hello_world()

// Attributes can be used to provide additional context to the compiler
// Typically, one is attached to a statement by prefixing it; for example, this tags this call to `hello_world()` with `amy.foo`
#[tag("amy.foo")]
println(hello_world());

// One can also attach an attribute to an entire scope, applying it to all statements in it;
// This...
{
    #[tag("bob.bar")]
    println(hello_world());
    #[tag("bob.bar")]
    println(hello_world());
}
// ...is equivalent to...
#[tag("bob.bar")]
{
    println(hello_world());
    println(hello_world());
}

// One can also use the `#![...]` syntax to attach an attribute to a whole scope from _within_ that scope
// This equals the previous example:
{
    #![tag("bob.bar")]
    println(hello_world());
    println(hello_world());
}

// We can use this latter fact to annotate an entire workflow...
#![wf_tag("cho.baz")]
// ...or all statements in the entire workflow (note this also applies to previous statements!)
#![tag("dan.qux")]
println(hello_world());
println(hello_world());



// Finally, there are some subteties to if-, for-, while- and parallel-statements
// When applied to the whole statement, it applies to *all* its blocks and *all* its expressions; i.e., in
#[tag("eve.quux")]
if (hello_world() == "Hello, world!") {
    println(hello_world());
} else {
    println(hello_world());
}
// all `hello_world()`-calls are executed with a `qaz`-tag. However, in this case;
if (hello_world() == "Hello, world!") {
    #![tag("eve.quux")]
    println(hello_world());
} else {
    println(hello_world());
}
// only the call in the true-branch is.
